#!/usr/bin/perl -w

#
# Copyright (C) 2006, 2007 INRIA (French National Institute for Research in
# Computer Science and Control)
#
# This library is free software; you can redistribute it and/or modify it under
# the terms of the GNU Lesser General Public License as published by the Free
# Software Foundation; either version 2.1 of the License, or (at your option)
# any later version.
#
# This library is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for more
# details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with this library; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301 USA
#

use strict;
use Getopt::Long;
use File::Basename;

#------------------------------------------------------------------------------
#           Outputs list of prime numbers as C header and source files
#------------------------------------------------------------------------------
# File    : genprimes.pl
# Author  : Jerome Milan
# Date    : Sat Mar 10 2007
# Version : 0.1.1
# Licence : GNU Lesser General Public License (LGPL) v2.1 or later
#           Copyright (C) 2006, 2007 INRIA
#-------------------------------------------------------------------------------
# History :
#-------------------------------------------------------------------------------
# 0.1.1: Sat Mar 10 2007 by JM:
#          - Added --gen option to generate the primes instead of reading
#            them from a file (GMP::Mpz module needed)
#          - Renamed genprimes.pl
# 0.1.0: Circa February 2006 by JM:
#          - Initial version
#------------------------------------------------------------------------------

#
# Options' declarations and default values, if any...
#
my $input_file      = "";
my $generate        = "";
my $output_prefix   = "";
my $nb_first_primes = 4096;
my $help            = "";
#
# Get the list of options...
#
GetOptions(
    "in=s"      => \$input_file,
    "gen"       => \$generate,
    "out=s"     => \$output_prefix,
    "nprimes=i" => \$nb_first_primes,
    "help"      => \$help
);
#
# ... and process them...
#
if ($help) {
    &print_help;
}
if (!$input_file && !$generate) {
    print("Error: no input file provided!\n");
    &print_usage;
}
if ($input_file && $generate) {
    print("Error: --in and --gen options are mutually exclusive!\n");
    &print_usage;
}
if (!$output_prefix) {
    print("Error: no output prefix provided!\n");
    &print_usage;
}
if ($input_file && ! -e $input_file) {
    print("Error: cannot read input file $input_file!\n");
    &print_usage;
}
if ($generate) {
    eval "use GMP::Mpz qw(:all); 1" or die "ERROR: GMP::Mpz not found!\n";
}
#
# Output filenames based on the given prefix...
#
my $output_inc = $output_prefix.".h";
my $output_src = $output_prefix.".c";
#
# Don't automatically overwrite the files if they exist...
#
if (-e $output_inc) {
    print("Error: output file $output_inc already exists!\n");
    &print_usage;
}
if (-e $output_src) {
    print ("Error: output file $output_src already exists!\n");
    &print_usage;
}
#
# Say what we do and do what we... oh wait!
#
if ($generate) {
    print("Generating $nb_first_primes prime numbers...\n");
} else {
    print("Reading $nb_first_primes numbers from $input_file\n");
}
print("Writing output in $output_inc and $output_src... ");
#
# Everything's ok? Then do some work!
#
my $name_prog = basename($0);
my $name_src  = basename($output_src);
my $name_inc  = basename($output_inc);
my $date      = localtime();
my $long_license = "//
// Copyright (C) 2006, 2007 INRIA (French National Institute for Research in
// Computer Science and Control)
//
// This library is free software; you can redistribute it and/or modify it under
// the terms of the GNU Lesser General Public License as published by the Free
// Software Foundation; either version 2.1 of the License, or (at your option)
// any later version.
//
// This library is distributed in the hope that it will be useful, but WITHOUT
// ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
// FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for more
// details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with this library; if not, write to the Free Software Foundation, Inc.,
// 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301 USA
//
";
#
# First, generate the source file...
#
open(OF, "> $output_src") or die("Error: cannot open file $output_src\n");

print OF << "EOF";
$long_license

/**
 * \\file    $name_src
 * \\author  Automatically generated by $name_prog
 * \\date    $date
 * \\version 1.0
 */

#include "$name_inc"

const uint32_t first_primes[NFIRST_PRIMES] __attribute__ ((unused)) = {
EOF

my $step  = 0;
my $prime = 1;

if ($input_file) {
    open(IF, "< $input_file") or die("Error: cannot open file $input_file\n");
    #
    # The prime numbers to be read just need to be separated by non digit
    # characters... Comments beginning by # are allowed...
    #
    # For example, on can use the files from http://primes.utm.edu/.
    #
    while ( defined(my $line = <IF>) and ($step < $nb_first_primes) ) {

        next if ($line =~ m/^(\s)*$/);       # Skips empty or blank lines
        next if ($line =~ m/^(\s)*\#.*$/);   # Skip comments...

        chomp($line);      # Suppress trailing newline if any
        $line =~ s/#.*//;  # Suppress trailing comments
        $line =~ s/^\D*//; # Suppress leading non digit characters
        $line =~ s/\D*$//; # Suppress trailing non digit characters

        my @primes = split(/\D+/, $line);
        foreach $prime (@primes) {
           if ($step != $nb_first_primes-1) {
             print OF "\t$prime,\n";
             $step++;
           } else {
             print OF "\t$prime\n";
             $step++;
             last
           }
        }
    }
    close(IF);
    if ($step != $nb_first_primes) {
        print("ERROR: Not enough primes defined in $input_file\n");
        unlink($output_src);
        exit(-1);
    }

} else {
    #
    # Generate the prime numbers like a good boy... with a little help
    # from the GMP::Mpz module...
    #
    for (1..$nb_first_primes-1) {
        $prime = nextprime($prime);
        print OF "\t$prime,\n";
    }
    $prime = nextprime($prime);
    print OF "\t$prime\n";
}
print(OF "};\n\n");

print OF << "EOF";
const uint32_t LARGEST_PRIME __attribute__ ((unused)) = $prime;

//
// _NOTE_: first_primes_array is merely an uint32_array_t wrapper for
//         first_primes, and as such, it has no real "alloced" memory.
//         Setting first_primes_array.alloced to 0 will prevent errors
//         if clear_uint32_array is called on first_primes_array.
//
const uint32_array_t first_primes_array __attribute__ ((unused)) = {
    .alloced = 0,
    .length  = NFIRST_PRIMES,
    .data    = (uint32_t*)first_primes
};

EOF

close(OF);

#
# ... and then, generates the header file...
#
my $base_prefix  = basename($output_prefix);
my $include_symb = "_TIFA_";
$include_symb .= "\U$base_prefix";  # to upper-case
$include_symb .= "_H_";
$include_symb  =~ s/\W/_/;          # replace non word characters by "_"

open(OF, "> $output_inc") or die("Error: cannot open file $output_inc\n");
print OF << "EOF";
$long_license

/**
 * \\file    $name_inc
 * \\author  Automatically generated by $name_prog
 * \\date    $date
 * \\version 1.0
 *
 * \\brief Precomputed small primes.
 *
 * This is a list of the precomputed small primes together with
 * a \\c uint32_array_t wrapper.
 */

#if !defined($include_symb)
   /**
    * \\def $include_symb
    * Standard include guard.
    */
#define $include_symb

#include <inttypes.h>

#include "array.h"

/**
 * \\def NFIRST_PRIMES
 * Number of precomputed primes in the \\c first_primes array.
 */
#define NFIRST_PRIMES $nb_first_primes

/**
 * The \\c first_primes array is a global array of \\c uint32_t elements
 * containing the first \\c NFIRST_PRIMES prime numbers (from 2 and beyond).
 */
extern const uint32_t first_primes[NFIRST_PRIMES] __attribute__ ((unused));

/**
 * The largest prime in the \\c first_primes array.
 */
extern const uint32_t LARGEST_PRIME __attribute__ ((unused));

/**
 * \\c first_primes_array is a \\c uint32_array_t wrapper to the array
 * \\c first_primes.
 *
 * \\note \\c first_primes_array 's \\c alloced field is set to zero. Indeed,
 * \\c first_primes_array is merely a \\c uint32_array_t wrapper for
 * \\c first_primes, and as such, it has no real "alloced" memory. Setting
 * \\c first_primes_array.alloced to 0 will prevent errors
 * if \\c clear_mpz_array is inadvertently called on \\c first_primes_array.
 */
extern const uint32_array_t first_primes_array __attribute__ ((unused));

#endif

EOF
close(OF);

printf("done!\n");

#
# Subroutines...
#

#--------------------------------------------------------------------
sub print_usage {
    my $name = basename($0);
    printf "Usage:\n";
    printf "------\n\n";
    printf "%15s [--in <input_file>|--gen] --out <output_prefix>\n", $name;
    printf "%15s --nprimes <number_of_primes> [--help]\n\n", '';
}
#--------------------------------------------------------------------
sub print_help {
    my $name = basename($0);

    my $title = "$name - Outputs list of primes as C header and source files";
    my $line  = '-' x length($title);
    print << "EOF";

$title
$line

EOF

    print_usage();

    print << "EOF";
This script is part of the TIFA (Tools for Integer FActorization) library.
It reads prime numbers from a file, or generates them, and declares them as
a uint32_t array in a C header and source file.

General parameters/options:
---------------------------

    --in
        Input file containing the prime numbers.

        The prime numbers to be read just need to be separated by non digit
        characters. Comments beginning by # are allowed.

        For example, one can use the files from http://primes.utm.edu/.

        If no input file is available, prime numbers can be generated via the
        --gen option.

    --gen
        Do not read prime numbers from an input file but generate them.
        This will obviously be somewhat slower if a large number of primes
        should be generated.

    --out
        Output file prefix. The header file and the C source file names
        are given by appending respectively ".h" and ".c".

    --nprimes
        Number of primes to read from the input file or to generate.

EOF
  exit();
}
#--------------------------------------------------------------------
